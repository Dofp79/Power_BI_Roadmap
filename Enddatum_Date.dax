Enddatum_Date = 
VAR DatumText =
    SUBSTITUTE(
        SUBSTITUTE(
            SUBSTITUTE(
                SUBSTITUTE(
                    SUBSTITUTE(
                        SUBSTITUTE(
                            SUBSTITUTE(
                                SUBSTITUTE(
                                    'Roadmap_ITIL'[Enddatum],
                                    "Montag, ", ""
                                ),
                                "Dienstag, ", ""
                            ),
                            "Mittwoch, ", ""
                        ),
                        "Donnerstag, ", ""
                    ),
                    "Freitag, ", ""
                ),
                "Samstag, ", ""
            ),
            "Sonntag, ", ""
        ),
        ".", "-"
    )
RETURN
    IFERROR(DATEVALUE(TRIM(DatumText)), BLANK())

//
// Dokumentation
// Tabelle:    Roadmap_ITIL
// Spalte:     Enddatum_Date
// Projekt:    ITIL - Roadmap
// Autor:      Doniman Francisco Peña Parra
// Erstellt:   03.05.2025
// Zweck:      Konvertiert ein Textdatum mit Wochentagsangabe in ein gültiges Datum
// Beschreibung:
// - Entfernt Wochentagsbezeichnungen (z. B. "Montag, ", "Dienstag, ", etc.)
// - Ersetzt Punkte durch Bindestriche für ein konsistentes Datumsformat
// - Wandelt das bereinigte Textdatum mit DATEVALUE in ein echtes Datum um
// - Gibt BLANK() zurück, wenn das Datum nicht interpretierbar ist
// - Ermöglicht die Nutzung als Zeitbasis in Zeitachsen und KPI-Berechnungen
// Prinzipien: Halte es einfach und praktisch | Beginne dort, wo du stehst
//
