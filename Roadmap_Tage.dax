Roadmap_Tage =
SELECTCOLUMNS(
    GENERATE(
        // Nur Zeilen mit gültigem Start- UND Enddatum verwenden
        FILTER(
            'Roadmap_ITIL',
            NOT ISBLANK('Roadmap_ITIL'[Startdatum_Date]) &&
            NOT ISBLANK('Roadmap_ITIL'[Enddatum_Date])
        ),
        // Für jede gültige Zeile eine Datumsreihe von Start bis Ende erzeugen
        ADDCOLUMNS(
            CALENDAR(
                'Roadmap_ITIL'[Startdatum_Date],
                'Roadmap_ITIL'[Enddatum_Date]
            ),
            // Weitere Projektinfos an jeden Tag anfügen
            "PhaseAktiv",     'Roadmap_ITIL'[Projektphase],
            "Status_Info",    'Roadmap_ITIL'[Status],
            "Reifegrad_Info", 'Roadmap_ITIL'[Reifegrad],
            "Team_Info",      'Roadmap_ITIL'[Team],
            "Risiko_Info",    'Roadmap_ITIL'[Risiko]
        )
    ),
    // Definiere die Spalten für die finale Tabelle
    "Datum",          [Date],
    "PhaseAktiv",     [PhaseAktiv],
    "Status_Info",    [Status_Info],
    "Reifegrad_Info", [Reifegrad_Info],
    "Team_Info",      [Team_Info],
    "Risiko_Info",    [Risiko_Info]
)
//
// Dokumentation
// Tabelle:    Roadmap_Tage
// Typ:        Berechnete Tabelle (Calculated Table)
// Projekt:    ITIL - Roadmap
// Autor:      Doniman Francisco Peña Parra
// Erstellt:   03.05.2025
// Zweck:      Erstellt tägliche Gantt-Zeilen pro Projektphase
// Beschreibung:
// - Nutzt GENERATE + CALENDAR, um für jede Projektphase tägliche Datumszeilen zu erzeugen
// - Filtert nur gültige Zeilen mit Start- und Enddatum
// - Fügt Metadaten wie Projektphase, Status, Reifegrad, Team und Risiko je Tag hinzu
// - Grundlage für Heatmaps, Zeitachsen, Sprintanalysen und Auslastungsdarstellungen
// Prinzipien: Denk und arbeite ganzheitlich | Fokussiere dich auf den Wert
//
